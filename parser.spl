// parser.spl

struct Parser (
  ast          : ptr,
  status       : u64,
  source_count : u64
);

let p : Parser 0;

fn parser_function -> ptr NULL;
let parse_entry parser_function;
let expression parser_function;
let statements parser_function;

fn parser_error(message : cstr) -> none {
  if eq load64 + @p Parser.status NoError {
    let tmp tmp_it;
    tmp_push_cstr("[parse-error]: ");
    tmp_push_cstr(load64 + @token Token.filename);
    tmp_push_cstr(":");
    tmp_push_u64(load64 + @token Token.line);
    tmp_push_cstr(":");
    tmp_push_u64(load64 + @token Token.column);
    tmp_push_cstr(": ");
    tmp_push_cstr(message);
    putsn(STDERR_FILENO, tmp, - tmp_it tmp);
    = @tmp_it tmp;
    store64 + @p Parser.status Error;
  }
}

fn expect_semicolon -> none {
  let type load64 + @token Token.type;
  if neq type T_SEMICOLON {
    parser_error("expected `;` semicolon, but got `?`\n");
  }
  else {
    lexer_next(); // skip `;`
  }
}

// L : `(` expr `,` ... `)`
//   | `(` empty `)`
fn parse_expr_list -> ptr {
  let expr_list ast_create(AstExprList);
  let type load64 + @token Token.type;
  if neq type T_RIGHT_P {
    let done 0;
    while eq done 0 {
      ast_push(expr_list, expression());
      = @type load64 + @token Token.type;
      if eq type T_COMMA {
        lexer_next();
      }
      else {
        = @done 1;
      }
    }
  }
  expr_list;
}

fn parse_type -> ptr {
  let type_expr : ptr NULL;
  let type load64 + @token Token.type;
  if or or or or or or
    eq type T_NONE
    eq type T_ANY
    eq type T_PTR
    eq type T_CSTR
    eq type T_UNSIGNED64
    eq type T_UNSIGNED32
    eq type T_IDENTIFIER {
    = @type_expr ast_create(AstType);
    memcpy(+ type_expr Ast.token, @token, sizeof Token);
    lexer_next(); // skip type
    = @type load64 + @token Token.type;
    if eq type T_COLON {
      lexer_next(); // skip `:`
      ast_push(type_expr, expression());
    }
  }
  type_expr;
}

fn parse_expr -> ptr {
  let expr : ptr NULL;
  let type load64 + @token Token.type;
  if or
    eq type T_NUMBER
    eq type T_CSTRING {
    = @expr ast_create(AstValue);
    memcpy(+ expr Ast.token, @token, sizeof Token);
    lexer_next();
  }
  else if eq type T_IDENTIFIER {
    let t : Token 0;
    memcpy(@t, @token, sizeof Token);
    lexer_next();
    = @type load64 + @token Token.type;
    if eq type T_LEFT_P { // function call
      lexer_next(); // skip `(`
      = @expr ast_create(AstFuncCall);
      memcpy(+ expr Ast.token, @t, sizeof Token);
      ast_push(expr, parse_expr_list());
      = @type lexer_token_type();
      if neq type T_RIGHT_P {
        parser_error("expected closing `)` parenthesis in function call, but got `?`\n");
      }
      else {
        lexer_next(); // skip `)`
      }
    }
    else {
      = @expr ast_create(AstValue);
      memcpy(+ expr Ast.token, @t, sizeof Token);
    }
  }
  else if or or or or or or or or or or or or
    eq type T_ADD
    eq type T_SUB
    eq type T_MUL
    eq type T_DIV
    eq type T_DIVMOD
    eq type T_LSHIFT
    eq type T_RSHIFT
    eq type T_LT
    eq type T_GT
    eq type T_AND
    eq type T_OR
    eq type T_EQ
    eq type T_NEQ {
    = @expr ast_create(AstBinopExpression);
    memcpy(+ expr Ast.token, @token, sizeof Token);
    lexer_next(); // skip operator
    ast_push(expr, parse_expr());
    ast_push(expr, parse_expr());
  }
  else if eq type T_AT {
    lexer_next();
    let t : Token 0;
    memcpy(@t, @token, sizeof Token);
    let type load64 + @t Token.type;
    if eq type T_IDENTIFIER {
      = @expr ast_create(AstValue);
      = + @t Token.type T_AT;
      memcpy(+ expr Ast.token, @t, sizeof Token);
      lexer_next(); // skip identifier
    }
    else {
      parser_error("expected identifier after `@`, but got `?`\n");
    }
  }
  else if or or or or or or
    eq type T_DEREF
    eq type T_LOAD64
    eq type T_LOAD32
    eq type T_LOAD16
    eq type T_LOAD8
    eq type T_PRINT
    eq type T_LOGICAL_NOT {
    let t : Token 0;
    memcpy(@t, @token, sizeof Token);
    lexer_next();
    = @expr ast_create(AstUopExpression);
    memcpy(+ expr Ast.token, @t, sizeof Token);
    ast_push(expr, parse_expr());
  }
  else if eq type T_SIZEOF {
    lexer_next();
    let type load64 + @token Token.type;
    let ok or or or or or or or or
      eq type T_ANY
      eq type T_PTR
      eq type T_NUMBER
      eq type T_UNSIGNED64
      eq type T_UNSIGNED32
      eq type T_CSTRING
      eq type T_CSTR
      eq type T_STRUCT
      eq type T_IDENTIFIER;

    if eq ok 0 {
      parser_error("expected type in sizeof operator, but got `?`\n");
    }
    else {
      = @expr ast_create(AstSizeof);
      memcpy(+ expr Ast.token, @token, sizeof Token);
      lexer_next(); // skip type
    }
  }
  else if eq type T_LEFT_P {
    lexer_next(); // skip `(`
    = @expr parse_expr();
    let type load64 + @token Token.type;
    if neq type T_RIGHT_P {
      parser_error("missing closing `)` parenthesis in expression\n");
    }
    else {
      lexer_next(); // skip `)`
    }
  }
  else if eq type T_RIGHT_P {
    parser_error("unexpected closing `)` parenthesis in expression\n");
  }
  else if eq type T_CAST {
    = @expr ast_create(AstCastExpression);
    memcpy(+ expr Ast.token, @token, sizeof Token);
    lexer_next(); // skip `cast`
    let inner_expr ast_create(AstExpression);
    ast_push(expr, parse_type());
    ast_push(inner_expr, parse_expr());
    ast_push(expr, inner_expr);
  }
  else if neq type T_EOF {
    parser_error("unexpected token `?` in expression\n");
    = + @p Parser.status Error;
  }
  expr;
}

fn parse_ident_list -> ptr {
  let ident_list : ptr ast_create(AstExprList);
  let type T_NONE;
  let t : Token 0;
  let done 0;
  while not done {
    memcpy(@t, @token, sizeof Token);
    = @type load64 + @token Token.type;
    if eq type T_IDENTIFIER {
      ast_push_node(ident_list, AstValue, @t);
      lexer_next(); // skip identifier
    }
    else {
      parser_error("expected identifier in identifier list, but got `?`\n");
      = @done 1;
    }

    = @type load64 + @token Token.type;
    if eq type T_COMMA {
      lexer_next(); // skip `,`
    }
    else {
      // we're done here if there was no comma
      = @done 1;
    }
  }
  ident_list;
}

fn parse_param_list -> ptr {
  let param_list : ptr ast_create(AstParamList);
  let type lexer_token_type();
  if neq type T_RIGHT_P {
    let done 0;
    while not done {
      = @type lexer_token_type();
      if eq type T_IDENTIFIER {
        let ident : Token 0;
        memcpy(@ident, @token, sizeof Token);
        lexer_next(); // skip argument identifier
        = @type lexer_token_type();
        if neq type T_COLON {
          parser_error("expected `:` colon after argument identifier, but got `?`\n");
          = @done 1;
        }
        else {
          lexer_next(); // skip `:`
          let arg : ptr ast_create(AstArg);
          memcpy(+ arg Ast.token, @ident, sizeof Token);
          let arg_type parse_type();
          if eq cast u64 arg_type NULL {
            parser_error("expected type after `:`, but got `?`\n");
            = @done 1;
          }
          else {
            ast_push(arg, arg_type);
            ast_push(param_list, arg);
            = @type lexer_token_type();
            if eq type T_COMMA {
              lexer_next(); // skip `,`
            }
            else {
              = @done 1;
            }
          }
        }
      }
      else {
        = @done 1;
      }
    }
  }
  param_list;
}

fn parse_struct -> ptr {
  let struct_expr : ptr ast_create(AstStruct);
  lexer_next(); // skip `struct`
  let type lexer_token_type();
  if neq type T_IDENTIFIER {
    parser_error("expected identifier after `struct` token, but got `?`\n");
  }
  else {
    memcpy(+ struct_expr Ast.token, @token, sizeof Token);
    lexer_next(); // skip struct name
    = @type lexer_token_type();
    if eq type T_LEFT_P {
      lexer_next(); // skip `(`
      let fields : ptr parse_param_list();
      ast_push(struct_expr, fields);
      if eq load64 + fields Ast.count 0 {
        parser_error("missing struct fields\n");
      }
      else {
        = @type lexer_token_type();
        if neq type T_RIGHT_P {
          parser_error("expected closing `)` parenthesis after struct field list, but got `?`\n");
        }
        lexer_next(); // skip `)`
      }
    }
    else {
      parser_error("expected open `(` parenthesis to begin struct field list, but got `?`\n");
    }
    expect_semicolon();
  }
  struct_expr;
}

fn parse_statement -> ptr {
  let stmt : ptr NULL;
  let type lexer_token_type();
  if or eq type T_CONST eq type T_LET {
    let branch_type : u64 type;
    let t : Token 0;
    lexer_next(); // skip const/let
    memcpy(@t, @token, sizeof Token);
    = @type load64 + @token Token.type;
    if neq type T_IDENTIFIER {
      parser_error("expected identifier in let/const statement, but got `?`\n");
    }
    else {
      lexer_next(); // skip identifier
      if eq branch_type T_LET {
        = @stmt ast_create(AstLetStatement);
      }
      else {
        = @stmt ast_create(AstConstStatement);
      }
      let explicit_type : ptr NULL;
      memcpy(+ stmt Ast.token, @t, sizeof Token); // copy identifier token
      = @type load64 + @token Token.type;
      if eq type T_COLON { // explicit type
        lexer_next(); // skip `:`
        = @explicit_type parse_type();
        if eq cast u64 explicit_type NULL {
          parser_error("expected type after `:`, but got `?`\n");
        }
        else {
          = @type load64 + + explicit_type Ast.token Token.type;
          if eq type T_NONE {
            parser_error("explicit type cannot be `none`\n");
          }
        }
      }

      // rhs of let/const statement
      = @type load64 + @token Token.type;
      if eq type T_LEFT_P {
        lexer_next(); // skip `(`
        ast_push(stmt, parse_expr_list());
        = @type load64 + @token Token.type;
        if neq type T_RIGHT_P {
          parser_error("expected closing `)` parenthesis in expression list, but got `?`\n");
        }
        lexer_next(); // skip `)`
      }
      else {
        ast_push(stmt, parse_expr_list());
      }

      ast_push(stmt, explicit_type);
      expect_semicolon();
    }
  }
  else if eq type T_MEMORY {
    lexer_next(); // skip `memory`
    = @type load64 + @token Token.type;
    if neq type T_IDENTIFIER {
      parser_error("expected identifier in memory statement, but got `?`\n");
    }
    else {
      = @stmt ast_create(AstMemoryStatement);
      memcpy(+ stmt Ast.token, @token, sizeof Token); // copy identifier token
      lexer_next(); // skip identifier
      ast_push(stmt, parse_expr());
      = @type load64 + @token Token.type;
      if neq type T_SEMICOLON {
        parser_error("expected `;` semicolon after memory statement, but got `?`\n");
      }
      else {
        lexer_next(); // skip `;`
      }
    }
  }
  else if eq type T_LEFT_CURLY {
    lexer_next(); // skip `{`
    = @stmt ast_create(AstBlockStatement);
    let sub_stmts statements();
    if neq cast u64 sub_stmts NULL {
      ast_push(stmt, sub_stmts);
      = @type load64 + @token Token.type;
      if neq type T_RIGHT_CURLY {
        parser_error("expected closing `}` curly bracket in block, but got `?`\n");
      }
      else {
        lexer_next(); // skip `}`
      }
    }
  }
  else if or or or or
    eq type T_ASSIGN
    eq type T_STORE64
    eq type T_STORE32
    eq type T_STORE16
    eq type T_STORE8 {
    = @stmt ast_create(AstAssignment);
    memcpy(+ stmt Ast.token, @token, sizeof Token);
    lexer_next(); // skip assignment/store operator
    ast_push(stmt, parse_expr());
    ast_push(stmt, parse_expr());
  }
  else if eq type T_WHILE {
    = @stmt ast_create(AstWhileStatement);
    memcpy(+ stmt Ast.token, @token, sizeof Token);
    lexer_next(); // skip `while`
    let cond ast_create(AstExpression);
    memcpy(+ cond Ast.token, @token, sizeof Token);
    ast_push(cond, parse_expr());
    ast_push(stmt, cond);
    = @type load64 + @token Token.type;
    if eq type T_LEFT_CURLY {
      lexer_next(); // skip `{`
      ast_push(stmt, statements());
      = @type load64 + @token Token.type;
      if neq type T_RIGHT_CURLY {
        parser_error("expected closing `}` curly bracket in while statement body, but got `?`\n");
      }
      else {
        lexer_next(); // skip `}`
      }
    }
    else {
      ast_push(stmt, parse_statement());
    }
  }
  else if eq type T_IF {
    = @stmt ast_create(AstIfStatement);
    memcpy(+ stmt Ast.token, @token, sizeof Token);
    lexer_next(); // skip `if`
    let cond ast_create(AstExpression);
    memcpy(+ cond Ast.token, @token, sizeof Token); // copy what ever token that is after `if`
    ast_push(cond, parse_expr());
    ast_push(stmt, cond);
    = @type load64 + @token Token.type;
    if eq type T_LEFT_CURLY {
      lexer_next(); // skip `{`
      ast_push(stmt, statements());
      = @type load64 + @token Token.type;
      if neq type T_RIGHT_CURLY {
        parser_error("expected closing `}` curly bracket in if statement body, but got `?`\n");
      }
      else {
        lexer_next(); // skip `}`
      }
    }
    else {
      let if_body ast_create(AstStatementList);
      ast_push(if_body, parse_statement());
      ast_push(stmt, if_body);
    }
    // parsing of else statements
    = @type load64 + @token Token.type;
    if eq type T_ELSE {
      lexer_next(); // skip `else`
      = @type load64 + @token Token.type;
      if eq type T_LEFT_CURLY {
        lexer_next(); // skip `{`
        ast_push(stmt, statements()); // parse else body
        = @type load64 + @token Token.type;
        if neq type T_RIGHT_CURLY {
          parser_error("expected closing `}` curly bracket in else body, but got `?`\n");
        }
        else {
          lexer_next(); // skip `}`
        }
      }
      else {
        let else_body ast_create(AstStatementList);
        ast_push(else_body, parse_statement());
        ast_push(stmt, else_body);
      }
    }
  }
  else if eq type T_ENUM {
    = @stmt ast_create(AstEnum);
    lexer_next(); // skip `enum`
    let type_branch : ptr parse_type();
    if eq cast u64 type_branch NULL {
      parser_error("expected type in enum expression, but got `?`\n");
    }
    else {
      ast_push(stmt, type_branch);
      = @type load64 + @token Token.type;
      if eq type T_LEFT_P {
        lexer_next(); // skip `(`
        ast_push(stmt, parse_ident_list());
        = @type load64 + @token Token.type;
        if neq type T_RIGHT_P {
          parser_error("expected closing `)` parenthesis in enum expression, but got `?`\n");
        }
        lexer_next(); // skip `)`
      }
      else {
        ast_push(stmt, parse_ident_list());
      }
      expect_semicolon();
    }
  }
  else if eq type T_STRUCT {
    = @stmt parse_struct();
  }
  else if eq type T_STATIC_ASSERT {
    = @stmt ast_create(AstStaticAssert);
    // memcpy(+ stmt Ast.token, @token, sizeof Token); // TODO(lucas): some strange behaviour happens here, bug, investigate!! could it something wrong with the token which is being copied here?
    lexer_next(); // skip `static_assert`
    ast_push(stmt, parse_expr());
    = @type load64 + @token Token.type;
    if neq type T_CSTRING {
      parser_error("expected string in static assert statement, but got `?`\n");
    }
    else {
      let node ast_create(AstValue);
      memcpy(+ node Ast.token, @token, sizeof Token);
      ast_push(stmt, node);
      lexer_next(); // skip string
    }
    expect_semicolon();
  }
  else if neq type T_EOF {
    = @stmt parse_expr();
    expect_semicolon();
  }
  stmt;
}

fn parse_func_def -> ptr {
  let func : ptr NULL;
  let rtype : ptr NULL;
  let type : u64 T_NONE;
  lexer_next(); // skip `fn`
  = @type lexer_token_type();
  if neq type T_IDENTIFIER {
    parser_error("expected identifier after `fn`, but got `?`\n");
  }
  else {
    = @func ast_create(AstFuncDefinition);
    memcpy(+ func Ast.token, @token, sizeof Token);
    lexer_next(); // skip function name

    // parse function parameter list
    = @type lexer_token_type();
    if eq type T_LEFT_P {
      lexer_next(); // skip `(`
      ast_push(func, parse_param_list());
      = @type lexer_token_type();
      if neq type T_RIGHT_P {
        parser_error("expected closing `)` parenthesis in function parameter list, but got `?`\n");
      }
      lexer_next();
    }
    else {
      ast_push(func, ast_create(AstParamList)); // push empty parameter list
    }

    // parse function return type
    = @type lexer_token_type();
    if eq type T_ARROW {
      lexer_next(); // skip `->`
      = @rtype parse_type();
      if eq cast u64 rtype NULL {
        parser_error("expected type after `->`, but got `?`\n");
      }
    }
    // parse function body
    = @type lexer_token_type();
    if eq type T_LEFT_CURLY {
      lexer_next(); // skip `{`
      ast_push(func, statements());
      = @type lexer_token_type();
      if neq type T_RIGHT_CURLY {
        parser_error("expected closing `}` curly bracket in function body, but got `?`\n");
      }
      lexer_next(); // skip `}`
    }
    else {
      let stmts : ptr ast_create(AstStatementList);
      ast_push(stmts, parse_expr());
      ast_push(func, stmts);
      expect_semicolon();
    }
    ast_push(func, rtype);
  }
  func;
}

fn parse_include -> ptr {
  let include_body : ptr NULL;
  lexer_next(); // skip `include`
  if eq lexer_token_type() T_CSTRING {
    let path load64 + @token Token.buffer;
    let path_length load64 + @token Token.length;
    let token_copy : Token 0;

    lexer_next(); // skip path string

    memcpy(@token_copy, @token, sizeof Token);

    // copy current lexer state
    let filename l.filename;
    let source l.source;
    let index l.index;
    let line l.line;
    let column l.column;
    let status l.status;

    let filename_path tmp_it;
    tmp_push_str(path, + 1 path_length);
    store8 cast ptr (+ filename_path path_length) 0;
    let fd open(filename_path, 0, O_RDONLY);
    if neq fd ERROR {
      let file_source tmp_it;
      let size read_file_into_buffer_and_null_terminate(fd, file_source);
      = @tmp_it + tmp_it size;
      lexer_init(filename_path, file_source);
      = @include_body parse_entry();
      = + @p Parser.source_count + 1 load64 + @p Parser.source_count;
      close(fd);
    }
    else {
      let message "";
      = @message tmp_it;
      tmp_push_cstr("failed to include source file `");
      tmp_push_str(path, path_length);
      tmp_push_cstr("`\n");
      parser_error(message);
      = @tmp_it message;
    }

    // restore lexer state
    = @l.filename filename;
    = @l.source source;
    = @l.index index;
    = @l.line line;
    = @l.column column;
    = @l.status status;

    // restore lexer token
    memcpy(@token, @token_copy, sizeof Token);
  }
  else {
    parser_error("expected string after `include` keyword\n");
  }
  include_body;
}

fn parse_statements -> ptr {
  let stmts ast_create(AstStatementList);
  let done 0;
  while not done {
    let type lexer_token_type();
    if eq type T_EOF {
      = @done 1;
    }
    else if eq type T_SEMICOLON {
      lexer_next();
    }
    else if eq type T_RIGHT_CURLY {
      = @done 1;
    }
    else if eq type T_FN {
      let func_def : ptr parse_func_def();
      if eq cast u64 func_def NULL {
        = @done 1;
      }
      else {
        ast_push(stmts, func_def);
      }
    }
    else if eq type T_INCLUDE {
      let include_body : ptr parse_include();
      if eq cast u64 include_body NULL {
        = @done 1;
      }
      else {
        ast_push(stmts, include_body);
      }
    }
    else {
      let stmt parse_statement();
      if eq cast u64 stmt NULL {
        = @done 1;
      }
      else {
        ast_push(stmts, stmt);
      }
    }
  }
  stmts;
}

fn parse -> ptr {
  lexer_next();
  let ast : ptr parse_statements();

  let message cast cstr tmp_it;
  let p tmp_push_cstr;
  p("parsing of `");
  p(l.filename);
  p("` took ? seconds\n");
  tmp_push_byte(0);
  print_info(message);
  = @tmp_it message;

  ast;
}

fn parser_init(filename : any, source : any) -> u64 {
  lexer_init(filename, source);
  = + @p Parser.ast ast_create(AstRoot);
  = + @p Parser.status NoError;
  = + @p Parser.source_count 0;

  = @parse_entry parse;
  = @expression parse_expr;
  = @statements parse_statements;

  NoError;
}

fn parser_free -> none {

}
