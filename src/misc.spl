// misc.spl

fn printline(fd: u64, source: cstr, index: cstr, token_length: u64, print_arrow: u64, num_lines_to_print: u64) -> none {
  assert(neq num_lines_to_print 0, "printline: value of num_lines_to_print must be greater than 0\n");
  assert(cast u64 and neq source null neq index null, "printline: source or index must not be null\n");

  let offset: u64 = + - cast u64 index source 1;
  let start_offset: u64 = 0;
  let cur_line_offset: u64 = 0;
  let end_offset: u64 = 0;
  let num_lines: u64 = num_lines_to_print;

  let at_index: cstr = index;
  // scan to the beginning of the line
  {
    let done: u64 = 0;
    while and eq done 0 < start_offset offset {
      store64 @start_offset + 1 start_offset;
      if eq num_lines_to_print num_lines {
        store64 @cur_line_offset + 1 cur_line_offset;
      }
      let ch: u64 = load8 - at_index start_offset;
      if eq ch 10 { // `\n`
        store64 @num_lines_to_print - num_lines_to_print 1;
        if eq num_lines_to_print 0 {
          store64 @done 1;
        }
      }
    }
  }

  // scan to the end of the line
  {
    let done: u64 = 0;
    while eq done 0 {
      let ch: u64 = load8 + at_index end_offset;
      if or eq ch 10 eq ch 0 { // `\n`, `\0`
        store64 @done 1;
      }
      else {
        store64 @end_offset + 1 end_offset;
      }
    }
  }

  const MAX_BUFFER_SIZE = 512;
  let buffer: u8[MAX_BUFFER_SIZE] = 0;
  let line_length: u64 = - + end_offset start_offset 1;
  let args: any = + - at_index start_offset 1;
  dnprintf(fd, line_length, "%s", @args);
  dprintf(fd, "\n", null);
  if print_arrow {
    {
      let i: u64 = 0;
      let count: u64 = - cur_line_offset 1;
      while < i count {
        dprintf(fd, " ", null);
        store64 @i + 1 i;
      }
    }
    {
      let i: u64 = 0;
      if eq token_length 0 {
        store64 @token_length 1;
      }
      color_set(COLOR_HINT);
      while < i token_length {
        dprintf(fd, "^", null);
        store64 @i + 1 i;
      }
      color_reset();
    }
    dprintf(fd, "\n", null);
  }
  dprintf(fd, "\n", null);
}

fn HERE dprintf(STDOUT_FILENO, "HERE\n", null);

fn err(message: cstr) -> none {
  color_set(COLOR_ERROR);
  dprintf(STDERR_FILENO, "[error]: ", null);
  color_reset();
  dprintf(STDERR_FILENO, "%s", @message);
}

fn error(message: cstr, args: any) -> none {
  color_set(COLOR_ERROR);
  dprintf(STDERR_FILENO, "[error]: ", null);
  color_reset();
  dprintf(STDERR_FILENO, message, args);
}

fn print_info(message: cstr) -> none {
  if load64 + @options Options.verbose {
    color_set(COLOR_INFO);
    dprintf(STDOUT_FILENO, "[info]: ", null);
    color_reset();
    dprintf(STDOUT_FILENO, message, null);
  }
}

fn exec_command_echoed(fd: u64, command_list: ptr, envp: ptr) -> none {
  if load64 + @options Options.verbose {
    let it = command_list;
    let done: u64 = 0;
    color_set(COLOR_INFO);
    dprintf(fd, "[cmd]: ", null);
    color_reset();
    while eq done 0 {
      if eq load64 it null {
        store64 @done 1;
      }
      else {
        dprintf(fd, "%s ", it);
        store64 @it + sizeof cstr it;
      }
    }
    dprintf(fd, "\n", null);
  }
  exec_command(command_list, envp);
}

fn get_time(spec: ptr<Timespec>) -> none {
  let _ = clock_gettime(CLOCK_MONOTONIC, spec);
}

fn print_time_elapsed(message: cstr, time_start: ptr<Timespec>, time_end: ptr<Timespec>) -> none {
  let nanoseconds = - load64 + time_end Timespec.tv_nsec load64 + time_start Timespec.tv_nsec;
  let seconds =     - load64 + time_end Timespec.tv_sec  load64 + time_start Timespec.tv_sec;
  let ms = + * 1000 seconds / nanoseconds 1000000;
  let info = tmp_it;
  let args: any = message, ms;
  sprintf(cast ptr info, "%s %d ms\n", @args);
  print_info(cast cstr info);
}
