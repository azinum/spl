// common.spl
// platform independent common/standard library

const ERROR - 0 2;
const NONE - 0 2;
const NoError 0;
const Error 1;
let NULL cast ptr 0;

const UINT64_MAX 0xffffffffffffffff;
const INT64_MAX  0x7fffffffffffffff;
const UINT32_MAX 0xffffffff;
const INT32_MAX  0x7fffffff;
const UINT16_MAX 0xffff;
const INT16_MAX  0x7fff;
const UINT8_MAX  0xff;
const INT8_MAX   0x7f;

const u64.max UINT64_MAX;
const i64.max INT64_MAX;
const u32.max UINT32_MAX;
const i32.max INT32_MAX;
const u16.max UINT16_MAX;
const i16.max INT16_MAX;
const u8.max  UINT8_MAX;
const i8.max  INT8_MAX;

fn strlen(str : cstr) -> u64 {
  let length : u64 0;
  while < 0 load8 str {
    = @length + 1 length;
    = @str    + 1 str;
  }
  length;
}

fn strcmp(a : cstr, b : cstr) -> u64 {
  let result 0;
  let done 0;
  while eq done 0 {
    let a_ch load8 a;
    let b_ch load8 b;
    if neq a_ch b_ch {
      = @result 1;
      = @done 1;
    }
    else if or eq a_ch 0 eq b_ch 0 {
      = @done 1;
    }
    = @a + 1 a;
    = @b + 1 b;
  }
  result;
}

fn strncmp(a : cstr, b : cstr, count : u64) -> u64 {
  let result 0;
  let i 0;
  let done 0;
  while and < i count eq done 0 {
    if neq load8 a load8 b {
      = @result 1;
      = @done 1;
    }
    else {
      = @a + 1 a;
      = @b + 1 b;
      = @i + 1 i;
    }
  }
  or neq load8 b 0 result;
}

fn memset(mem : ptr, c : u64, n : u64) -> none {
  let count : u64 0;
  while < count n {
    store8 mem c;
    = @mem   + 1 mem;
    = @count + 1 count;
  }
}

fn memset8(mem : any, c : u64, n : u64) -> none {
  let count : u64 0;
  while < count n {
    store64 mem c;
    = @mem   + sizeof u64 mem;
    = @count + sizeof u64 count;
  }
}

fn memcpy(dest : any, src : any, n : u64) -> none {
  let count : u64 0;
  while < count n {
    store8 dest load8 src;
    = @dest  + 1 dest;
    = @src   + 1 src;
    = @count + 1 count;
  }
}

fn digit_count(n : u64) -> u64 {
  const base : u64 10;
  let count : u64 1;
  while > n 9 {
    = @n / n base;
    = @count + 1 count;
  }
  count;
}

fn u64_to_str(n : u64, str : any, max_size : u64) -> none {
  const base : u64 10;
  let count : u64 digit_count(n);
  let i : u64 0;

  = @str + str - count 1;

  while and (< i count) (< i max_size) {
    let index % n base;
    store8 str load8 + index "0123456789abcdef";
    = @n   / n base;
    = @str - str 1;
    = @i   + 1 i;
  }
}

// TODO: implement parsing of hex numbers
// TODO: add error checking
fn str_to_u64(str : cstr, length : u64) -> u64 {
  const base : u64 10;
  let result : u64 0;
  let i : u64 0;
  while < i length {
    let ch load8 + str i;
    = @result + * result base - ch '0';
    = @i + 1 i;
  }
  result;
}

include "lib/linux.spl"
